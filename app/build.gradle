apply plugin: "com.android.application"
apply plugin: "kotlin-android"
// TODO: the 'kotlin-android-extensions' Gradle plugin is deprecated. Please use this migration
//  guide (https://goo.gle/kotlin-android-extensions-deprecation) to start working with View
//  Binding (https://developer.android.com/topic/libraries/view-binding) and the 'kotlin-parcelize'
//  plugin.
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "com.google.firebase.firebase-perf"

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "de.lorenzgorse.coopmobile"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 84
        versionName "1.84"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }
    signingConfigs {
        release {
            def keystorePropsFile = project.file("keystore.properties")
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(keystorePropsFile))
                storeFile file(keystoreProps["storeFile"])
                storePassword keystoreProps["storePassword"]
                keyAlias keystoreProps["keyAlias"]
                keyPassword keystoreProps["keyPassword"]
            } catch (FileNotFoundException e) {
                println("Did not find keystore properties at " + keystorePropsFile)
            }
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions{
        // TODO: consider enabling this again
        disable "MissingTranslation"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "true")
        }
        debug {
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "false")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"

    implementation project(path: ':coopclient')
    implementation project(path: ':encryption')

    implementation "com.google.android.material:material:1.6.0-alpha02"

    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.0-alpha02"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.fragment:fragment-ktx:1.5.0-alpha02"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.room:room-runtime:2.4.1"
    implementation 'androidx.test.ext:junit-ktx:1.1.3'
    kapt "androidx.room:room-compiler:2.4.1"

    implementation platform("com.google.firebase:firebase-bom:29.1.0")
    implementation "com.google.firebase:firebase-config"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"
    implementation 'com.google.firebase:firebase-config-ktx'

    implementation "org.slf4j:slf4j-api:1.7.32"
    implementation "com.github.tony19:logback-android:2.0.0"
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "com.google.android.play:core:1.10.3"
    implementation "com.google.code.gson:gson:2.8.6"

    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    testImplementation "androidx.test:core-ktx:1.4.0"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "io.mockk:mockk:1.12.3"
}
