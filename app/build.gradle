plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.firebase-perf"
    id "com.google.devtools.ksp"
}

kotlin {
    jvmToolchain 11
}

android {
    namespace "de.lorenzgorse.coopmobile"
    compileSdk 34
    defaultConfig {
        applicationId "de.lorenzgorse.coopmobile"
        minSdk 26
        targetSdk 34
        versionCode 110
        versionName "2.0000110"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }
    signingConfigs {
        release {
            def keystorePropsFile = project.file("keystore.properties")
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(keystorePropsFile))
                storeFile file(keystoreProps["storeFile"])
                storePassword keystoreProps["storePassword"]
                keyAlias keystoreProps["keyAlias"]
                keyPassword keystoreProps["keyPassword"]
            } catch (FileNotFoundException e) {
                println("Did not find keystore properties at " + keystorePropsFile)
            }
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        // TODO: consider enabling this again
        disable "MissingTranslation"
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "true")
        }
        debug {
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "false")
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"

    implementation project(path: ':coopclient')
    implementation project(path: ':encryption')
    implementation project(path: ':bifrost')

    implementation "com.google.android.material:material:1.12.0"

    implementation "com.google.dagger:dagger:2.47"
    kapt "com.google.dagger:dagger-compiler:2.47"

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.8.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.fragment:fragment-ktx:1.7.1"
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.room:room-runtime:2.6.1"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.test.ext:junit-ktx:1.1.5'
    ksp "androidx.room:room-compiler:2.5.2"

    implementation platform("com.google.firebase:firebase-bom:29.1.0")
    implementation "com.google.firebase:firebase-config"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"
    implementation 'com.google.firebase:firebase-config-ktx'

    // TODO: logging doesn't work anymore (seems to use NOP logger)
    implementation "org.slf4j:slf4j-api:2.0.7"
    implementation "com.github.tony19:logback-android:2.0.0"

    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "com.google.android.play:core:1.10.3"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "io.arrow-kt:arrow-core:1.1.2"
    implementation("com.google.protobuf:protobuf-javalite:3.25.3")
    implementation("io.grpc:grpc-okhttp:1.64.0")
    implementation("io.grpc:grpc-stub:1.64.0")

    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "io.mockk:mockk:1.12.3"
}
