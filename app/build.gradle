apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "de.lorenzgorse.coopmobile"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 35
        versionName "1.35"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def keystorePropsFile = project.file("keystore.properties")
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(keystorePropsFile))
                storeFile file(keystoreProps["storeFile"])
                storePassword keystoreProps["storePassword"]
                keyAlias keystoreProps["keyAlias"]
                keyPassword keystoreProps["keyPassword"]
            } catch (FileNotFoundException e) {
                println("Did not find keystore properties at " + keystorePropsFile)
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
        }
        debug {
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        String sharedTestDir = "src/sharedTest/java"
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.google.android.material:material:1.3.0-alpha01"

    implementation "androidx.appcompat:appcompat:1.3.0-alpha01"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.2.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.2.2"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.fragment:fragment-ktx:1.3.0-alpha06"
    implementation "androidx.core:core-ktx:1.3.0"

    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "com.github.tony19:logback-android:2.0.0"
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "org.jsoup:jsoup:1.12.2"
    implementation "com.squareup.okhttp3:okhttp:4.4.0"
    implementation "com.google.firebase:firebase-config:19.1.4"
    implementation "com.google.firebase:firebase-analytics:17.4.3"
    implementation "com.google.firebase:firebase-crashlytics:17.0.1"

    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    testImplementation "junit:junit:4.13"
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation "org.mockito:mockito-inline:3.1.0"

    androidTestImplementation "junit:junit:4.13"

    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"

    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.2.0"
    debugImplementation "androidx.fragment:fragment-testing:1.2.5"
}
